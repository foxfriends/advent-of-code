import "trilogy:io" use readlines
import "trilogy:debug" use dbg
import "trilogy:core" use to_array, length
import "trilogy:bits" as bits
import "trilogy:compare" use min
import "trilogy:parsec" use parse, apply, between, many_1, char_of, char, integer, sep_by_1
import "trilogy:array" use map, fold, filter, reduce, all, prepend
import "trilogy:set" use contains, difference, union, collect
import "trilogy:iterator" as it

func target_state '.' = false
func target_state '#' = true

func set_bit bb index = bb | (0bb1 ~~> index)

func button_bits len button = fold set_bit (bits::init_zero len) button

proc machine!() {
  let target = bits::from_array
    <| map target_state
    <| apply
    <| between (char '[') (char ']')
    <| many_1
    <| char_of ".#"
  apply <| char ' '
  let buttons = apply
    <| sep_by_1 (char ' ')
    <| between (char '(') (char ')')
    <| sep_by_1 (char ',') integer
  apply <| char ' '
  let joltage = apply
    <| between (char '{') (char '}')
    <| sep_by_1 (char ',') integer
  return joltage:buttons
}

func choose 0 _ = do() yield 'next([])
func choose _ [] = do() {}
func choose n [x, ..xs] = do() {
  let within = choose (n - 1) xs |> it::map (prepend x)
  let without = choose n xs
  within!()
  without!()
}

func decr_at 0 [m, ..arr] = [m - 1, ..arr]
func decr_at i [m, ..arr] = [m, ..decr_at (i - 1) arr]

func apply_button target [] = target
func apply_button target [i, ..rest] = apply_button (decr_at i target) rest

func solve_bits buttons target =
  it::rangex 1 (length buttons)
  |> it::flat_map (fn n. choose n buttons)
  |> it::filter (((==) target) << (reduce (^)) << map (button_bits (length target)))

proc solve!(joltage, buttons) {
  if all ((==) 0) joltage {
    return 0
  }
  return joltage
  |> map (fn n. n % 2 == 1)
  |> bits::from_array
  |> solve_bits buttons
  |> it::map (fn sol. fold apply_button joltage sol : length sol)
  |> it::filter (fn remaining:_. all (fn n. n >= 0) remaining)
  |> it::map (fn remaining:score. solve!(map (fn n. n // 2) remaining, buttons) * 2 + score)
  |> it::map dbg
  |> it::reduce min
}

proc main!() {
  readlines
  |> it::map (parse machine)
  |> it::map dbg
  |> it::map (fn j:b. solve!(j, b))
  |> it::map dbg
  |> it::fold (+) 0
  |> dbg
}
