import "trilogy:io" use readlines
import "trilogy:debug" use dbg
import "trilogy:core" use to_array, length
import "trilogy:bits" as bits
import "trilogy:record" use collect, merge_by, contains_key
import "trilogy:compare" use min
import "trilogy:parsec" use parse, apply, between, many_1, char_of, char, integer, sep_by_1
import "trilogy:array" use map, fold, filter
import "trilogy:iterator" as it

func target_state '.' = false
func target_state '#' = true

func set_bit bb index = bb | (0bb1 ~~> index)

proc machine!() {
  let target = bits::from_array
    <| map target_state
    <| apply
    <| between (char '[') (char ']')
    <| many_1
    <| char_of ".#"
  apply <| char ' '
  let buttons = map (fold set_bit (target ^ target))
    <| apply
    <| sep_by_1 (char ' ')
    <| between (char '(') (char ')')
    <| sep_by_1 (char ',') integer
  apply <| char ' '
  let _joltage = apply
    <| between (char '{') (char '}')
    <| sep_by_1 (char ',') integer
  return target:buttons
}

func press_button round states button = states
  |> to_array
  |> filter (fn state:presses. presses == round)
  |> map (fn state:presses. state ^ button : presses + 1)
  |> it::from
  |> collect

proc solve!(target:buttons) {
  let mut states = {| target ^ target => 0 |}
  let mut i = 0
  while !(contains_key target states) {
    states = map (press_button i states) buttons
      |> fold (merge_by (fn k a b. min a b)) states
    i += 1
  }
  return states.target
}

proc main!() {
  readlines
  |> it::map (parse machine)
  |> it::map solve
  |> it::fold (+) 0
  |> dbg
}
