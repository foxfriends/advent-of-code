import "trilogy:debug" use dbg
import "trilogy:io" use readlines
import "trilogy:number" use abs
import "trilogy:parsec" use parse, char_of, integer, apply
import "trilogy:iterator" as it

proc rotation!() {
  let dir = apply <| char_of "LR"
  let amt = apply integer
  return if dir == 'R' then amt else -amt
}

func rotate pos amt =
  let full = abs amt // 100,
  let rotated = pos + amt % 100,
  let crossed = rotated < 0 || rotated > 99,
  yield 'rotations(full + if crossed then 1 else 0);
  (rotated + 100) % 100

proc main!() {
  let mut answer = 0
  with readlines
    |> it::map (parse rotation)
    |> it::foldl rotate 50
    when 'rotations(n) then {
      answer += n
      become unit
    }
    else yield
  dbg!(answer)
}
