module io at "trilogy:io" use println, readlines
module str at "trilogy:str" use chomp, chars
module array at "trilogy:array" use split, length, all, slice
module iter at "trilogy:iter" use rangex
module num at "trilogy:num" use min

func transpose arr = [[arr.y.x for y in rangex 0 (length arr)] for x in rangex 0 (length (arr.0))]

func palindrome [] = true
func palindrome [_] = true
func palindrome [x, ..xs, x] = palindrome xs
func palindrome _ = false

func reflect_around gap line =
    let to_close_edge = min gap (length line - gap),
    let part = slice (gap - to_close_edge) (to_close_edge * 2) line,
    palindrome part


proc main!() {
    let lines = split ((==) "") [chomp line for line in readlines!()]
    let puzzles = [[chars line for line in puzzle] for puzzle in lines]

    let mut total = 0
    # Doing horizontal first
    for puzzle in $(transpose p for p in puzzles) {
        for gap in rangex 1 (length (puzzle.0)) {
            if all (reflect_around gap) puzzle {
                total += gap
                break
            }
        }
    }

    total *= 100

    # Vertical second
    for puzzle in puzzles {
        for gap in rangex 1 (length (puzzle.0)) {
            if all (reflect_around gap) puzzle {
                total += gap
                break
            }
        }
    }

    println!(total)
}
