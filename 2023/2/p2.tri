import "trilogy:debug" use dbg
import "trilogy:io" use readlines
import "trilogy:compare" use max
import "trilogy:array" use fold, flatten
import "trilogy:iterator" as it
import "trilogy:record" use update
import "trilogy:parsec" use parse, apply, string, integer, char, sep_by_1, prefixed_by, choice

slot init = {| "red" => 0, "green" => 0, "blue" => 0 |}

func max_color n:col rec = update (max n) 0 col rec

func power {| "red" => r, "green" => g, "blue" => b |} = r * g * b

proc draw!() {
  let count = apply integer
  apply <| char ' '
  let color = apply <| choice [string "red", string "blue", string "green"]
  return count:color
}

proc game!() {
  apply <| string "Game "
  apply integer
  apply <| string ": "
  return apply
    <| sep_by_1 (string "; ")
    <| sep_by_1 (string ", ")
    <| draw
}

proc main!() {
    readlines
    |> it::map <| parse game
    |> it::map <| fold max_color init << flatten
    |> it::map power
    |> it::reduce (+)
    |> dbg
}
