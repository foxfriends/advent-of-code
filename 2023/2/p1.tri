import "trilogy:debug" use dbg
import "trilogy:io" use readlines
import "trilogy:array" use map, all
import "trilogy:tuple" use left, right
import "trilogy:iterator" as it
import "trilogy:parsec" use parse, apply, string, integer, char, sep_by_1, prefixed_by, choice

func valid n:"red"   = n <= 12
func valid n:"green" = n <= 13
func valid n:"blue"  = n <= 14

proc draw!() {
  let count = apply integer
  apply <| char ' '
  let color = apply <| choice [string "red", string "blue", string "green"]
  return count:color
}

proc game!() {
  apply <| string "Game "
  let index = apply integer
  apply <| string ": "
  let draws = apply
    <| sep_by_1 (string "; ")
    <| sep_by_1 (string ", ")
    <| draw
  return index:draws
}

proc main!() {
    readlines
    |> it::map <| parse game
    |> it::filter <| all (all valid) << right
    |> it::map left
    |> it::reduce (+)
    |> dbg
}
