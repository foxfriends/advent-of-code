module io at "trilogy:io" use readlines, println
module num at "trilogy:num" use re, im, cast, min
module str at "trilogy:str" use chomp, chars, fmt
module array at "trilogy:array" use merge, length
module tuple at "trilogy:tuple" use fst, snd
module btreemod at "trilogy:btree" use btree
module iter at "trilogy:iter" use range

slot N = -1i
slot S = 1i
slot E = 1
slot W = -1

func turn dir = [-1i * dir, -(-1i * dir)]
func key ^N = 'v
func key ^S = 'v
func key ^E = 'h
func key ^W = 'h

func asc key lhs rhs = key lhs <= key rhs

func insert _ val unit = val:unit
func insert _ val val:xs = val:xs
func insert cmp val x:xs = if cmp val x then val:x:xs else x:insert cmp val xs

proc main!() {
    let grid = [[cast $"${n}"fmt for n in chars <| chomp line] for line in readlines!()]
    let best = {| 0 => {| 'h => 0, 'v => 0 |} |}

    let goal = (length grid * S + length (grid.0) * E) + N + W

    let score = fn pos:dir. best.pos.(key dir)
    let enqueue = fn pos dir. insert (asc score) (pos:dir)

    let ltree = btree (asc score)

    let mut queue = ltree::from [0:E, 0:S]
    while queue != 'empty {
        let (pos:face):q2 = ltree::pop_min queue
        queue = q2
        let fhv = key face
        if pos == goal {
            println!(best.pos.fhv)
            exit 0
        }
        for dir in turn face {
            let dhv = key dir
            let mut offset = 0
            let cont_outer = (continue)
            for i in range 1 10 {
                let newpos = pos + i * dir
                offset += with grid.(im newpos).(re newpos) else invert { break }
                if i <= 3 { continue }
                let newscore = best.pos.fhv + offset
                best.newpos = with best.newpos else cancel {| |}
                with {
                    if newscore < best.newpos.dhv {
                        queue = ltree::remove (newpos:dir) queue
                        best.newpos.dhv = newscore
                        queue = ltree::insert (newpos:dir) queue
                    }
                } else invert {
                    best.newpos.dhv = newscore
                    queue = ltree::insert (newpos:dir) queue
                    cancel
                }
            }
        }
    }

    exit 1
}
