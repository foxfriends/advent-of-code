import "trilogy:io" use readline
import "trilogy:debug" use dbg
import "trilogy:iterator" as it
import "trilogy:array" use collect, take, skip, length
import "trilogy:heap" use heap
import "trilogy:record" use contains_key
import "trilogy:compare" use min

slot GOAL = [
  ['open, 'open, 'empty, 'open, 'empty, 'open, 'empty, 'open, 'empty, 'open, 'open],
  ['A', 'A'],
  ['B', 'B'],
  ['C', 'C'],
  ['D', 'D'],
]

func replace pos val array = [
  ..take pos array,
  val,
  ..skip (pos + 1) array,
]

func cost 'A' = 1
func cost 'B' = 10
func cost 'C' = 100
func cost 'D' = 1000

proc eject!(value, position, depth, [spaces, ..stacks]) {
  for pos in collect <| it::range position 10 {
    match spaces.pos {
      case 'open then yield 'next([replace pos value spaces, ..stacks]:cost value * (pos - position + depth))
      case 'empty then continue unit
      else break unit
    }
  }
  for pos in collect <| it::range position 0 {
    match spaces.pos {
      case 'open then yield 'next([replace pos value spaces, ..stacks]:cost value * (position - pos + depth))
      case 'empty then continue unit
      else break unit
    }
  }
}

proc fall_in!(value, position, depth, [spaces, ..stacks]) {
  for pos in collect <| it::range position 10 {
    match spaces.pos {
      case ^value then yield 'next([replace pos 'open spaces, ..stacks]:cost value * (pos - position + depth))
      case 'empty then continue unit
      case 'open then continue unit
    }
    break unit
  }
  for pos in collect <| it::range position 0 {
    match spaces.pos {
      case ^value then yield 'next([replace pos 'open spaces, ..stacks]:cost value * (position - pos + depth))
      case 'empty then continue unit
      case 'open then continue unit
    }
    break unit
  }
}

func next_states [spaces, aa, bb, cc, dd] = do() {
  match aa {
    case ['A', 'A'] {}
    case ['A'] then fall_in!('A', 2, 1, [spaces, ['A', 'A'], bb, cc, dd])
    case [] then fall_in!('A', 2, 2, [spaces, ['A'], bb, cc, dd])
    case [..aas, v] then eject!(v, 2, 2 - length aas, [spaces, aas, bb, cc, dd])
  }
  match bb {
    case ['B', 'B'] {}
    case ['B'] then fall_in!('B', 4, 1, [spaces, aa, ['B', 'B'], cc, dd])
    case [] then fall_in!('B', 4, 2, [spaces, aa, ['B'], cc, dd])
    case [..bbs, v] then eject!(v, 4, 2 - length bbs, [spaces, aa, bbs, cc, dd])
  }
  match cc {
    case ['C', 'C'] {}
    case ['C'] then fall_in!('C', 6, 1, [spaces, aa, bb, ['C', 'C'], dd])
    case [] then fall_in!('C', 6, 2, [spaces, aa, bb, ['C'], dd])
    case [..ccs, v] then eject!(v, 6, 2 - length ccs, [spaces, aa, bb, ccs, dd])
  }
  match dd {
    case ['D', 'D'] {}
    case ['D'] then fall_in!('D', 8, 1, [spaces, aa, bb, cc, ['D', 'D']])
    case [] then fall_in!('D', 8, 2, [spaces, aa, bb, cc, ['D']])
    case [..dds, v] then eject!(v, 8, 2 - length dds, [spaces, aa, bb, cc, dds])
  }
}

proc main!() {
  readline!()
  readline!()
  let upper = readline!()
  let lower = readline!()
  let initial = [
    ['open, 'open, 'empty, 'open, 'empty, 'open, 'empty, 'open, 'empty, 'open, 'open],
    [lower.3, upper.3],
    [lower.5, upper.5],
    [lower.7, upper.7],
    [lower.9, upper.9],
  ]
  let min_heap = heap (fn _:a _:b. a <= b)
  let queue = [initial:0]
  let cache = {|"${initial}" => 0|}
  while queue != [] {
    let state:score = min_heap::pop!(queue)
    if cache."${state}" < score {
      continue unit
    }
    if state == GOAL {
      dbg!(score)
      exit 0
    }
    next_states state
      |> it::map (fn state:cost. state:cost + score)
      |> it::for_each do(entry and next_state:next_score) {
        let key = "${next_state}"
        if !(contains_key key cache) || cache.key > next_score {
          cache.key = next_score
          min_heap::push!(queue, entry)
        }
      }
  }
}
